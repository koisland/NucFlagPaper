import glob
from os.path import join, dirname


OUTPUT_DIR = config["output_dir"]
LOG_DIR = config["log_dir"]
BENCHMARK_DIR = config["benchmark_dir"]
NUCFLAG_CONFIG = config["nucflag_config"]
ASM_COLORS = {
    "CHM13_hifiasm_0.25.0": "orange",
    "CHM13_verkko_2.2.1": "blue",
    "CHM13_verkko_2.3": "teal",
}


rule download_chm13:
    output:
        fa=join(OUTPUT_DIR, "reference", "chm13v2.0.fa.gz"),
        fai=join(OUTPUT_DIR, "reference", "chm13v2.0.fa.gz.fai"),
    params:
        url="https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/CHM13/assemblies/analysis_set/chm13v2.0.fa.gz",
    conda:
        "../../envs/curated.yaml"
    shell:
        """
        wget {params.url} -O {output.fa}
        samtools faidx {output.fa}
        """


module DenovoAssembly:
    snakefile:
        "Snakemake-Assembly/workflow/Snakefile"
    config:
        config


use rule * from DenovoAssembly as denovo_*


def get_reads_from_glob(data_src: dict) -> list[str]:
    data_dir = data_src["path"]
    glob_include = data_src["include"][0]
    return glob.glob(join(data_dir, glob_include))


# Then run NucFlag on each assembly
nf_config = {
    "samples": [
        {
            # Must have no underscores.
            # Format: SM_verkko or SM_hifiasm
            "name": f"{sm}_{dtype}",
            "asm_fa": (
                expand(rules.denovo_verkko_output.output, asm=sm.split("_")[1], sm=sm)[
                    0
                ]
                if sm.split("_")[1] == "verkko"
                else expand(
                    rules.denovo_hifiasm_output.output, asm=sm.split("_")[1], sm=sm
                )[0]
            ),
            "reads": get_reads_from_glob(config["samples"][sm]["data"][dtype]),
            "config": NUCFLAG_CONFIG[dtype],
        }
        for sm in config["samples"].keys()
        for dtype in ["hifi", "ont"]
    ],
    "output_dir": join(OUTPUT_DIR, "nucflag"),
    "output_plots": False,
    "output_pileup": False,
    "output_ideogram": True,
    "output_breakdown": True,
    "logs_dir": join(LOG_DIR, "nucflag"),
    "benchmarks_dir": join(BENCHMARK_DIR, "nucflag"),
    "threads_aln": 24,
    "mem_aln": "50G",
    "processes_nucflag": 24,
    "threads_nucflag": 12,
    "mem_nucflag": "50G",
    # Filter unmapped, non-primary, and supplementary alignments.
    "samtools_view_flag": 2308,
}


module NucFlagDenovoAsm:
    snakefile:
        "../Snakemake-NucFlag/workflow/Snakefile"
    config:
        nf_config


use rule * from NucFlagDenovoAsm as nf_denovo_*


include: "asm_variant_graph.smk"
include: "compare_homopolymers.smk"
include: "liftover.smk"
include: "draw_ng50.smk"
include: "compare_all.smk"


rule all:
    input:
        rules.denovo_all.input,
        rules.vg_all.input,
        rules.cmp_homopolymers_all.input,
        rules.nf_denovo_nucflag.input,
        rules.draw_ng50.output,
        rules.liftover_all.input,
