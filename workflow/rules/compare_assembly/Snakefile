import glob
from os.path import join


OUTPUT_DIR = config["output_dir"]
LOG_DIR = config["log_dir"]
BENCHMARK_DIR = config["benchmark_dir"]


module DenovoAssembly:
    snakefile:
        "Snakemake-Assembly/workflow/Snakefile"
    config:
        config


use rule * from DenovoAssembly as denovo_*


def get_reads_from_glob(data_src: dict) -> list[str]:
    data_dir = data_src["path"]
    glob_include = data_src["include"][0]
    return glob.glob(join(data_dir, glob_include))


# Then run NucFlag on each assembly
nf_config = {
    "samples": [
        {
            "name": sm,
            "asm_fa": (
                expand(rules.denovo_hifiasm_output.output, asm=asm, sm=sm)[0]
                if asm == "verkko"
                else expand(rules.denovo_hifiasm_output.output, asm=asm, sm=sm)[0]
            ),
            "reads": get_reads_from_glob(config["samples"][sm]["data"][dtype]),
            "preset": dtype,
        }
        for sm in config["samples"].keys()
        for asm in ["verkko", "hifiasm"]
        for dtype in ["hifi", "ont"]
    ],
    "output_dir": join(OUTPUT_DIR, "nucflag"),
    "output_plots": False,
    "output_pileup": False,
    "output_ideogram": True,
    "output_breakown": True,
    "logs_dir": join(LOG_DIR, "nucflag"),
    "benchmarks_dir": join(BENCHMARK_DIR, "nucflag"),
    "threads_aln": 24,
    "mem_aln": "50G",
    "processes_nucflag": 24,
    "threads_nucflag": 12,
    "mem_nucflag": "50G",
    # Filter unmapped, non-primary, and supplementary alignments.
    "samtools_view_flag": 2308,
}


module NucFlagDenovoAsm:
    snakefile:
        "../Snakemake-NucFlag/workflow/Snakefile"
    config:
        nf_config


use rule * from NucFlagDenovoAsm as nf_denovo_*


rule all:
    input:
        rules.denovo_all.input,
        rules.nf_denovo_nucflag.input,
