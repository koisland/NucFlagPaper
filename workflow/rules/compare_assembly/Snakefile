import glob
from os.path import join, dirname


OUTPUT_DIR = config["output_dir"]
LOG_DIR = config["log_dir"]
BENCHMARK_DIR = config["benchmark_dir"]
NUCFLAG_CONFIG = config["nucflag_config"]


module DenovoAssembly:
    snakefile:
        "Snakemake-Assembly/workflow/Snakefile"
    config:
        config


use rule * from DenovoAssembly as denovo_*


def get_reads_from_glob(data_src: dict) -> list[str]:
    data_dir = data_src["path"]
    glob_include = data_src["include"][0]
    return glob.glob(join(data_dir, glob_include))


# TODO: Liftover cytobands (https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/CHM13/assemblies/annotation/chm13v2.0_cytobands_allchrs.bed)
# TODO: Liftover censat (https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/CHM13/assemblies/annotation/chm13v2.0_censat_v2.1.bed)


# Then run NucFlag on each assembly
nf_config = {
    "samples": [
        {
            # Must have no underscores.
            # Format: SM_verkko or SM_hifiasm
            "name": f"{sm}_{dtype}",
            "asm_fa": (
                expand(rules.denovo_verkko_output.output, asm=sm.split("_")[1], sm=sm)[
                    0
                ]
                if sm.split("_")[1] == "verkko"
                else expand(
                    rules.denovo_hifiasm_output.output, asm=sm.split("_")[1], sm=sm
                )[0]
            ),
            "reads": get_reads_from_glob(config["samples"][sm]["data"][dtype]),
            "config": NUCFLAG_CONFIG[dtype],
        }
        for sm in config["samples"].keys()
        for dtype in ["hifi", "ont"]
    ],
    "output_dir": join(OUTPUT_DIR, "nucflag"),
    "output_plots": False,
    "output_pileup": False,
    "output_ideogram": True,
    "output_breakdown": True,
    "logs_dir": join(LOG_DIR, "nucflag"),
    "benchmarks_dir": join(BENCHMARK_DIR, "nucflag"),
    "threads_aln": 24,
    "mem_aln": "50G",
    "processes_nucflag": 24,
    "threads_nucflag": 12,
    "mem_nucflag": "50G",
    # Filter unmapped, non-primary, and supplementary alignments.
    "samtools_view_flag": 2308,
}


module NucFlagDenovoAsm:
    snakefile:
        "../Snakemake-NucFlag/workflow/Snakefile"
    config:
        nf_config


use rule * from NucFlagDenovoAsm as nf_denovo_*


include: "asm_variant_graph.smk"
include: "compare_homopolymers.smk"


rule all:
    input:
        rules.denovo_all.input,
        rules.vg_all.input,
        rules.cmp_homopolymers_all.input,
        rules.nf_denovo_nucflag.input,
