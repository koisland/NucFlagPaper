include: "../common.smk"


OUTPUT_DIR = config["output_dir"]
IDEOGRAM_OUTPUT_DIR = join(OUTPUT_DIR, "ideogram")
ELEMENT_OUTPUT_DIR = join(OUTPUT_DIR, "element")
BENCHMARK_DIR = config["benchmarks_dir"]
LOG_DIR = config["logs_dir"]
HIFI = config["hifi"]
CONFIG = config["config"]
CONFIG_ELEMENT = config["config_element"]
FLAGGER_ALPHA = config["alpha"]


VCFS = {
    "v1.0.1": "https://github.com/marbl/HG002-issues/raw/refs/heads/main/patches/already_applied/hg002v1.0.1_to_hg002v1.1.vcf.gz",
    "v0.9": "https://github.com/marbl/HG002-issues/raw/refs/heads/main/patches/already_applied/hg002v0.9_to_hg002v1.0.vcf.gz",
    "v0.7": "https://github.com/marbl/HG002-issues/raw/refs/heads/main/patches/already_applied/hg002v0.7_to_hg002v0.9.vcf.gz",
}
ASSEMBLIES = {
    "v1.0.1": "https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/HG002/assemblies/hg002v1.0.1.fasta.gz",
    "v0.9": "https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/HG002/assemblies/hg002v0.9.fasta.gz",
    "v0.7": "https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/HG002/assemblies/hg002v0.7.fasta",
}
ANNOTATIONS_V101 = [
    "https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/HG002/assemblies/annotation/segdups/HG002.SDs.010624.45col.bb",
    "https://s3-us-west-2.amazonaws.com/human-pangenomics/T2T/HG002/assemblies/annotation/centromere/hg002v1.0.1_v2.0/hg002v1.0.1.cenSatv2.0.noheader.bb",
    "https://42basepairs.com/download/s3/human-pangenomics/T2T/HG002/assemblies/annotation/cytoband/cytoBand.hg002v1.0.bb",
]
ELEMENT = {
    "R1": "s3://human-pangenomics/T2T/scratch/HG002/sequencing/element/trio/HG002/ins1000/ASHG-C063_R1.fastq.gz",
    "R2": "s3://human-pangenomics/T2T/scratch/HG002/sequencing/element/trio/HG002/ins1000/ASHG-C063_R2.fastq.gz",
}


wildcard_constraints:
    version="|".join(ASSEMBLIES.keys()),


rule download_vcf:
    output:
        join(OUTPUT_DIR, "data", "vcf", "{version}.vcf.gz"),
    params:
        url=lambda wc: VCFS[wc.version],
    shell:
        """
        wget {params.url} -O {output}
        """


rule convert_vcf_to_bed:
    input:
        rules.download_vcf.output,
    output:
        os.path.join(OUTPUT_DIR, "data", "{version}_truth.bed"),
    shell:
        """
        zcat -f {input} | grep -v "#" | awk -v OFS="\\t" '{{
            print $1, $2, $2 + length($4), $4"-"$5, 0, $3, $2, $2 + length($4), "0,0,0"
        }}' > {output}
        """


rule download_curated_asm:
    output:
        fa=join(OUTPUT_DIR, "data", "asm", "{version}.fa.gz"),
        fai=join(OUTPUT_DIR, "data", "asm", "{version}.fa.gz.fai"),
    params:
        url_asm=lambda wc: ASSEMBLIES[wc.version],
    shell:
        """
        wget {params.url_asm} -O {output.fa}
        wget {params.url_asm}.fai -O {output.fai}
        """


rule download_element:
    output:
        reads_1=join(OUTPUT_DIR, "data", "reads", "ASHG-C063_R1.fastq.gz"),
        reads_2=join(OUTPUT_DIR, "data", "reads", "ASHG-C063_R2.fastq.gz"),
    params:
        uri_1=ELEMENT["R1"],
        uri_2=ELEMENT["R2"],
    conda:
        "../../envs/curated.yaml"
    shell:
        """
        aws s3 --no-sign-request cp {params.uri_1} {output.reads_1}
        aws s3 --no-sign-request cp {params.uri_2} {output.reads_2}
        """


config = {
    "samples": [
        {
            "name": f"HG002_{version}",
            "asm_fa": expand(rules.download_curated_asm.output.fa, version=version)[0],
            "reads": HIFI,
            "config": CONFIG,
        }
        for version, data in ASSEMBLIES.items()
    ],
    "output_dir": OUTPUT_DIR,
    "output_plots": False,
    "output_ideogram": True,
    "output_breakdown": True,
    "logs_dir": LOG_DIR,
    "benchmarks_dir": BENCHMARK_DIR,
    "threads_aln": 24,
    "mem_aln": "50G",
    "processes_nucflag": 12,
    "threads_nucflag": 12,
    "mem_nucflag": "50G",
    # Filter unmapped, non-primary, and supplementary alignments.
    "samtools_view_flag": 2308,
}


module NucFlagCurated:
    snakefile:
        "../Snakemake-NucFlag/workflow/Snakefile"
    config:
        config


use rule * from NucFlagCurated as versioned_*


flagger_config = {
    "output_dir": join("results", "curated", "flagger"),
    "benchmarks_dir": join("benchmarks", "curated", "flagger"),
    "logs_dir": join("logs", "curated", "flagger"),
    "samples": [
        {
            "name": f"HG002_{version}",
            "asm_fa": expand(rules.download_curated_asm.output.fa, version=version)[0],
            "bam": rules.versioned_aln_merge_read_asm_alignments.output.alignment.format(
                sm=f"HG002_{version}"
            ),
            "alpha": FLAGGER_ALPHA,
        }
        for version, data in ASSEMBLIES.items()
    ],
}

inspector_config = {
    "output_dir": join("results", "curated", "inspector"),
    "benchmarks_dir": join("benchmarks", "curated", "inspector"),
    "logs_dir": join("logs", "curated", "inspector"),
    "samples": [
        {
            "name": f"HG002_{version}",
            "asm_fa": expand(rules.download_curated_asm.output.fa, version=version)[0],
            "bam": rules.versioned_aln_merge_read_asm_alignments.output.alignment.format(
                sm=f"HG002_{version}",
            ),
            "datatype": "hifi",
        }
        for version, data in ASSEMBLIES.items()
    ],
}


deepvariant_config = {
    "output_dir": join("results", "curated", "deepvariant"),
    "benchmarks_dir": join("benchmarks", "curated", "deepvariant"),
    "logs_dir": join("logs", "curated", "deepvariant"),
    "samples": [
        {
            "name": f"HG002_{version}",
            "asm_fa": expand(rules.download_curated_asm.output.fa, version=version)[0],
            "bam": rules.versioned_aln_merge_read_asm_alignments.output.alignment.format(
                sm=f"HG002_{version}",
            ),
        }
        for version, data in ASSEMBLIES.items()
    ],
}


module Inspector:
    snakefile:
        "../inspector.smk"
    config:
        inspector_config


use rule * from Inspector as versioned_*


module Flagger:
    snakefile:
        "../flagger.smk"
    config:
        flagger_config


use rule * from Flagger as versioned_*


module DeepVariant:
    snakefile:
        "../deepvariant.smk"
    config:
        deepvariant_config


use rule * from DeepVariant as versioned_*


def get_bed_files(wc):
    if wc.tool == "nucflag":
        return expand(
            rules.versioned_check_asm_nucflag.output.misassemblies,
            sm=f"HG002_{wc.version}",
        )
    elif wc.tool == "flagger":
        return expand(rules.versioned_run_flagger.output, sm=f"HG002_{wc.version}")
    elif wc.tool == "inspector":
        return expand(rules.versioned_merge_calls.output, sm=f"HG002_{wc.version}")
    elif wc.tool == "deepvariant":
        return expand(rules.versioned_filter_vcf_calls.output, sm=f"HG002_{wc.version}")
    else:
        raise ValueError(f"Invalid tool. {wc.tool}")


rule calculate_precision_recall:
    input:
        script="workflow/scripts/metrics/calculate_precision_recall_curated.py",
        test_bed=get_bed_files,
        truth_bed=rules.convert_vcf_to_bed.output,
    output:
        summary=join(OUTPUT_DIR, "summary", "{tool}_{version}.tsv"),
        missed_calls_dir=directory(
            join(OUTPUT_DIR, "summary", "{tool}_{version}_missed")
        ),
    conda:
        "../../envs/tools.yaml"
    shell:
        """
        python {input.script} \
        -a {input.test_bed} \
        -b {input.truth_bed} \
        --output_dir_missed_calls {output.missed_calls_dir} > {output.summary}
        """


# Create venn-diagram shared between callers
# Shows that NucFlag picks up a lot more than other callers.
rule plot_call_venn:
    input:
        nucflag=expand(
            rules.versioned_check_asm_nucflag.output.misassemblies,
            sm="HG002_{version}",
        )[0],
        flagger=expand(rules.versioned_run_flagger.output, sm="HG002_{version}")[0],
        inspector=expand(rules.versioned_merge_calls.output, sm="HG002_{version}")[0],
        deepvariant=expand(
            rules.versioned_filter_vcf_calls.output, sm="HG002_{version}"
        )[0],
    output:
        plot=join(OUTPUT_DIR, "call_ovl", "overlap_{version}.png"),
    params:
        script="workflow/scripts/curated/call_venn_diagram.py",
        json_inputs=lambda wc, input: json.dumps(dict(input)),
    shell:
        """
        python {params.script} -i '{params.json_inputs}' -o {output.plot}
        """


# TODO: Consensus precision/recall as more callers added.


include: "generate_ideogram.smk"
include: "validate_homopolymers.smk"
include: "qv.smk"


rule all:
    input:
        rules.versioned_nucflag.input,
        rules.versioned_flagger.input,
        rules.versioned_inspector.input,
        rules.versioned_deepvariant.input,
        expand(rules.convert_vcf_to_bed.output, version=ASSEMBLIES.keys()),
        expand(rules.plot_call_venn.output, version=ASSEMBLIES.keys()),
        expand(
            rules.calculate_precision_recall.output,
            version=ASSEMBLIES.keys(),
            tool=["flagger", "inspector", "nucflag", "deepvariant"],
        ),
        rules.generate_ideogram.input,
        rules.validate_homopolymers_all.input,
        rules.qv_run_all.input,
    default_target: True
